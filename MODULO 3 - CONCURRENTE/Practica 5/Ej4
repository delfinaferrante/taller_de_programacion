programa Ej4
procesos
  proceso depositarFlores(E flores:numero)
  comenzar
    mientras(flores > 0)
      depositarFlor
      flores:= flores - 1
  fin
  proceso irADepositar(E flores:numero; E avInicio:numero; E calleInicio:numero)
  comenzar
    BloquearEsquina(10,10)
    Pos(10,10)
    depositarFlores(flores)
    Pos(avInicio,calleInicio)
    LiberarEsquina(10,10)
  fin
  proceso juntarFlores(ES flores:numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      flores:= flores + 1
  fin
  proceso irAJuntarFlores(ES flores:numero; E av:numero; E calle:numero; E avInicio:numero; E calleInicio:numero)
  comenzar
    BloquearEsquina(av,calle)
    Pos(av,calle)
    juntarFlores(flores)
    Pos(avInicio,calleInicio)
    LiberarEsquina(av,calle)
  fin
areas
  area1: AreaP (1,1,1,1)
  area2: AreaP (2,1,2,1)
  area3: AreaP (3,1,3,1)
  cuadrante: AreaPC (40,40,60,60)
  deposito: AreaPC (10,10,10,10)
  Afiscalizador: AreaP (4,1,4,1)
robots
  robot floreros
  variables
    intentos,av,calle,flores,avInicio,calleInicio,id:numero
    sigue:boolean
  comenzar
    flores:= 0
    avInicio:= PosAv
    calleInicio:= PosCa   
    
    RecibirMensaje(id, rfisc)
    RecibirMensaje(sigue, rfisc)
    mientras(sigue)
      RecibirMensaje(av,rfisc)
      RecibirMensaje(calle,rfisc)  
      irAJuntarFlores(flores,av,calle,avInicio,calleInicio)
      RecibirMensaje(sigue, rfisc)
      
    {cuando ya se realizaron 8 intentos}
    irADepositar(flores, avInicio,calleInicio)  
    EnviarMensaje(flores, rfisc)
  fin
  robot fiscalizador
  variables
    av,calle,intentos,idRandom,flores,totalFlores,id:numero
    sigue:boolean
  comenzar
    totalFlores:= 0
    intentos:= 0
    sigue:= V
    EnviarMensaje(1, r1)
    EnviarMensaje(2, r2)
    EnviarMensaje(3, r3)
    
    mientras(intentos < 8)
      Random(av,40,60)
      Random(calle,40,60)
      Random(id, 1, 3)
      si(id = 1)
        EnviarMensaje(sigue,r1)
        EnviarMensaje(av,r1)
        EnviarMensaje(calle,r1)
      sino
        si(id = 2)
          EnviarMensaje(sigue,r2)
          EnviarMensaje(av,r2)
          EnviarMensaje(calle,r2)
        sino
          EnviarMensaje(sigue,r3)
          EnviarMensaje(av,r3)
          EnviarMensaje(calle,r3)        
      intentos:= intentos + 1
      
    sigue:= F  {aviso que terminaron los 8 intentos}
    EnviarMensaje(sigue, r1)
    EnviarMensaje(sigue, r2)
    EnviarMensaje(sigue, r3)
    repetir 3
      RecibirMensaje(flores, *)
      totalFlores:= totalFlores + flores
    Informar(totalFlores)
  fin
variables
  r1,r2,r3: floreros
  rfisc: fiscalizador
comenzar
  AsignarArea(r1, cuadrante)
  AsignarArea(r2, cuadrante)
  AsignarArea(r3, cuadrante)
  AsignarArea(r1, deposito)
  AsignarArea(r2, deposito)
  AsignarArea(r3, deposito)
  AsignarArea(r1,area1)
  AsignarArea(r2,area2)
  AsignarArea(r3,area3)
  AsignarArea(rfisc, Afiscalizador)
  Iniciar(r1, 1,1)
  Iniciar(r2, 2,1)
  Iniciar(r3, 3,1)
  Iniciar(rfisc, 4,1)
fin