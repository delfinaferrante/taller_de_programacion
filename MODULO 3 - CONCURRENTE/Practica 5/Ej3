programa Ej3
procesos
  proceso vaciarBolsa(ES flores:numero)
  comenzar
    mientras((HayFlorEnLaBolsa)&(flores > 0))
      depositarFlor
      flores:= flores - 1
  fin
  proceso irAlDeposito(ES flores:numero)
  variables
    avAct,calleAct:numero
  comenzar
    avAct:= PosAv
    calleAct:= PosCa
    BloquearEsquina(10,10)
    Pos(10,10)
    vaciarBolsa(flores)  
    Pos(avAct,calleAct)
    LiberarEsquina(10,10)  
  fin
  proceso juntarFlores(ES flores:numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      flores:= flores + 1
  fin
  proceso voyAJuntarFlores(E avInicio:numero; E calleInicio:numero;ES totalFlores:numero)
  comenzar
    BloquearEsquina(10,10)
    Pos(10,10)
    juntarFlores(totalFlores)
    Pos(avInicio,calleInicio)
    LiberarEsquina(10,10)
  fin
areas
  area1: AreaP (1,3,100,3)
  area2: AreaP (1,4,100,4)
  area3: AreaP (1,5,1,5)
  deposito: AreaC (10,10,10,10)
robots
  robot recolectores
  variables
    flores, esquinas:numero
    termino:boolean
  comenzar
    termino:= F 
    derecha
    mientras (PosAv < 100) {mientras no termino toda la calle}
      esquinas:= 0
      flores:= 0  
      mientras ( (PosAv < 100) & (flores<10) & (esquinas < 15))
        si(HayFlorEnLaEsquina)
          tomarFlor
          flores:= flores + 1
        si(PosAv < 100)
          mover
          esquinas:= esquinas + 1    
      irAlDeposito(flores)
      EnviarMensaje(F, rjefe)  {avisa que ya deposito flores}     
    EnviarMensaje(V,rjefe)     {avisa que termino de recorrer toda la calle}
  fin
  robot cosechador
  variables
    confirmacion:boolean
    totalFlores,flores,avInicio,calleInicio,robotsTerminados:numero
  comenzar
    robotsTerminados:= 0
    flores:= 0
    totalFlores:= 0
    avInicio:= PosAv
    calleInicio:= PosCa
    
    mientras(robotsTerminados < 2)
      RecibirMensaje(confirmacion, *)
      si ~(confirmacion)                         {si es F, deposito flores pero todavia hay av disponibles}
        voyAJuntarFlores(avInicio,calleInicio, flores)
        totalFlores:= totalFlores + flores
      sino                                       {si es V, termino de recorrer todo}
        robotsTerminados:= robotsTerminados + 1
    Informar(totalFlores)
  fin
variables
  r1,r2: recolectores
  rjefe: cosechador
comenzar
  AsignarArea(r1, area1)
  AsignarArea(r2, area2)
  AsignarArea(rjefe, area3)
  AsignarArea(r1, deposito)
  AsignarArea(r2, deposito)
  AsignarArea(rjefe, deposito)
  Iniciar(r1, 1,3)
  Iniciar(r2, 1,4)
  Iniciar(rjefe, 1,5)
fin